dependencies {
    if (!project.hasProperty("Legacy")) {
        api fileTree(dir: "libs", includes: ['jshader.jar'])
    }

    implementation 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
}

allprojects {
    repositories {
        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        //maven {
        //    url "http://nexus.hc.to/content/repositories/pub_releases/"
        //}
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {
        api project(":core")

        testImplementation project(':core').sourceSets.test.output
    }

    jar {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE

        from {
            String[] include = [
                    "${project(':core').archivesBaseName}-${project.version}.jar",
                    "${project(':bukkit').archivesBaseName}-${project.version}.jar",
                    "commons-lang3-3.12.0.jar"
            ]

            configurations.runtimeClasspath
                    .findAll { include.contains(it.name) }
                    .collect { it.isDirectory() ? it : zipTree(it) }
        }

        dependsOn project(':core').jar
    }
}

subprojects {
    dependencies {
        implementation fileTree(dir: "libs", includes: ['*.jar'])
        api project(":bukkit")

        testImplementation project(':bukkit').sourceSets.test.output
    }

    task copyTestResources(type: Copy) {
        from project(":bukkit").sourceSets.main.output.resourcesDir
        into "${buildDir}/classes/test"
    }
    processTestResources.dependsOn copyTestResources

    configurations {
        TestDependNashorn
        TestDependGraal
    }

    dependencies {
        TestDependNashorn 'org.openjdk.nashorn:nashorn-core:15.3'
        // https://mvnrepository.com/artifact/org.graalvm.js/js
        TestDependGraal 'org.graalvm.js:js:21.1.0'
        // https://mvnrepository.com/artifact/org.graalvm.js/js-scriptengine
        TestDependGraal 'org.graalvm.js:js-scriptengine:21.1.0'
    }

    task testWithNashorn(type: Test) {
        classpath += configurations.TestDependNashorn
    }

    task testWithGraal(type: Test) {
        classpath += configurations.TestDependGraal
    }
}

test {
    // not for tests
    exclude '**/Abstract*.class'
}